#! /usr/bin/gawk -f

function url_hexify_string(str) {
    if (system("command -v jq > /dev/null")) {
        print "jq is not found, install it from https://stedolan.github.io/jq/" > "/dev/stderr"
        exit(1)
    }
    command = "printf '%s' " str " | jq -R -r @uri"
    command | getline
    close(command)
    return $0
}

BEGIN {
    if (ARGC != 2) {
        printf "Usage: %s QUERY\n", ARGV[0] > "/dev/stderr"
        exit(1)
    }

    http_service = "/inet/tcp/0/fanyi.youdao.com/80"
    query = url_hexify_string(ARGV[1])
    path = "/openapi.do?keyfrom=YouDaoCV&key=659600698&type=data&doctype=json&version=1.1&q=" query

    printf("GET %s HTTP/1.1\r\nHost: fanyi.youdao.com\r\n\r\n", path) |& http_service

    RS = "\r\n"                 # the record separator
    FS = ": "                   # the field separator

    while ((http_service |& getline) > 0) {
        if ($1 == "Content-Length")
            content_length = int($2)
        else if ($0 == "Transfer-Encoding: chunked")
            chunked = 1
        else if ($0 ~ /^$/ )
            break
    }

    if (chunked) content_length = 0

    while ((http_service |& getline) > 0) {
        if (/^$/)
            break
        # https://en.wikipedia.org/wiki/Chunked_transfer_encoding
        else if (chunked && ++i % 2) # XXX: $0 should be a hex number, check against /^[0-9a-f]+$/?
            content_length += strtonum("0x"$0)
        else
            buf = buf $0
    }

    close(http_service)
    # XXX: Prettify the output
    print substr(buf, 0, content_length)
}
